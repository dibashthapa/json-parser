JSON-text       = ws value ws

value           = "null" | "true" | "false"
                | object | array | number | string

object          = "{" ws "}"
                | "{" ws member *( ws "," ws member ) ws "}"

member          = string ws ":" ws value

array           = "[" ws "]"
                | "[" ws value *( ws "," ws value ) ws "]"

number          = ["-"] int [ frac ] [ exp ]

int             = "0"
                | digit1-9 *digit

frac            = "." +digit

exp             = ("e" | "E") ["-" | "+"] +digit

string          = '"' *char '"'

char            = unescaped
                | "\" escape

escape          = '"' | "\" | "/" | "b" | "f" | "n" | "r" | "t"
                | "u" hex hex hex hex

hex             = digit | "A"…"F" | "a"…"f"

unescaped       = %x20-21 / %x23-5B / %x5D-10FFFF
                (any Unicode except " or \ or control)

digit           = %x30-39
digit1-9        = %x31-39

ws              = *(%x20 / %x09 / %x0A / %x0D)   ; space, tab, lf, cr
